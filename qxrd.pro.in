TEMPLATE = app
CONFIG += qt

VERSION = @VERSION@

QT += network script

#INCLUDEPATH += /usr/local/lib/spec.d/include/

MOC_DIR = moc
UI_DIR  = ui
OBJECTS_DIR = obj
RCC_DIR = rcc

TARGET = qxrd

RESOURCES += qxrdhelptext.qrc 

DISTFILES += qxrdhelptext.html

INCLUDEPATH += ../qceplib/

HEADERS = \
	../qceplib/spec_server.h \
	../qceplib/qspecserver.h \
	qxrdapplication.h \
	qxrdsettings.h \
	../qceplib/qcepimagedata.h \
	../qceplib/qcepimagedataformat.h \
	../qceplib/qcepimagedataformatfactory.h \
	qxrdimagedata.h \
	qxrdrasterdata.h \
	qxrdwindow.h \
	qxrdimageplot.h \
	qxrdserverthread.h \
	qxrdserver.h \
	qxrdspecserver.h \
	qxrdacquisitionthread.h \
	qxrdacquisition.h

SOURCES = \
	qxrd.cpp \
	../qceplib/qspecserver.cpp \
	qxrdapplication.cpp \
	qxrdsettings.cpp \
	../qceplib/qcepimagedata.cpp \
	../qceplib/qcepimagedataformat.cpp \
	../qceplib/qcepimagedataformatfactory.cpp \
	qxrdimagedata.cpp \
	qxrdrasterdata.cpp \
	qxrdwindow.cpp \
	qxrdimageplot.cpp \
	qxrdserverthread.cpp \
	qxrdserver.cpp \
	qxrdspecserver.cpp \
	qxrdacquisitionthread.cpp \
	qxrdacquisition.cpp

FORMS = \
	qxrdwindow.ui

INCLUDEPATH += /usr/include/qwt/
LIBS += -lqwt 
#-lAqDrv4 -lCom -lca
#QMAKE_CXXFLAGS += -D_LINUX -D_ACQIRIS

QMAKE_EXTRA_TARGETS += dox rpmsource mock

dox.target = docs
dox.commands += doxygen
dox.depends = Doxyfile $${SOURCES} $${HEADERS}

rpmsource.commands += make dist ;
rpmsource.commands += cp qxrd@VERSION@.tar.gz ~/rpmbuild/SOURCES/
rpmsource.depends = $${DISTFILES} $${SOURCES} $${FORMS} $${HEADERS} $${RESOURCES}

mock.commands += mock-build qxrd.spec
mock.depends = rpmsource ~/rpmbuild/SOURCES/qxrd@VERSION@.tar.gz qxrd.spec
