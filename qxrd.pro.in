TEMPLATE = app
CONFIG += qt

VERSION = @VERSION@

QT += network script

#INCLUDEPATH += /usr/local/lib/spec.d/include/

MOC_DIR = moc
UI_DIR  = ui
OBJECTS_DIR = obj
RCC_DIR = rcc

TARGET = qxrd

RESOURCES += qxrdhelptext.qrc 

DISTFILES += qxrdhelptext.html

INCLUDEPATH += ../qceplib/

HEADERS = \
	../qceplib/spec_server.h \
	../qceplib/qspecserver.h \
	qxrdapplication.h \
	qxrdsettings.h \
	../qceplib/qcepimagedata.h \
	../qceplib/qcepimagedataformat.h \
	../qceplib/qcepimagedataformatfactory.h \
	qxrdimagedata.h \
	qxrdrasterdata.h \
	qxrdwindow.h \
	qxrdimageplot.h \
	qxrdserverthread.h \
	qxrdserver.h \
	qxrdspecserver.h \
	qxrdacquisitionthread.h \
	qxrdacquisition.h \
	Acq.h

SOURCES = \
	qxrd.cpp \
	../qceplib/qspecserver.cpp \
	qxrdapplication.cpp \
	qxrdsettings.cpp \
	../qceplib/qcepimagedata.cpp \
	../qceplib/qcepimagedataformat.cpp \
	../qceplib/qcepimagedataformatfactory.cpp \
	qxrdimagedata.cpp \
	qxrdrasterdata.cpp \
	qxrdwindow.cpp \
	qxrdimageplot.cpp \
	qxrdserverthread.cpp \
	qxrdserver.cpp \
	qxrdspecserver.cpp \
	qxrdacquisitionthread.cpp \
	qxrdacquisition.cpp

FORMS = \
	qxrdwindow.ui

unix{
  SOURCES += xisl_dummy.cpp
  INCLUDEPATH += /usr/include/qwt/
  LIBS += -lqwt 
}

win32{
  PLATFORM_PREFIX=win32
  QTBINDIR=C:/Qt/4.4.0/bin/
  QWT=C:/Qwt-5.1.1/
  app.target = app
  zip.target = zip
  CONFIG(debug, debug|release) {
    LIBS += -L$${QWT}/lib/ -lqwt_debug

	app.commands = C:/cygwin/bin/cp.exe \
                        $${QTBINDIR}/QtCored4.dll \
                        $${QTBINDIR}/QtNetworkd4.dll \
                        $${QTBINDIR}/QtGuid4.dll \
                        $${QTBINDIR}/QtScriptd4.dll \
                        $${QWT}/lib/qwt_debug5.dll \
                        XISL.dll \
                        C:/MinGW/bin/mingwm10.dll \
                        debug
   
    zip.commands = C:/cygwin/bin/zip.exe -j $${TARGET}_debug.zip \
			debug/$${TARGET}.exe \
                        $${QTBINDIR}/QtCored4.dll \
                        $${QTBINDIR}/QtNetworkd4.dll \
                        $${QTBINDIR}/QtGuid4.dll \
                        $${QTBINDIR}/QtScriptd4.dll \
                        $${QWT}/lib/qwt_debug5.dll \
                        XISL.dll \
                        C:/MinGW/bin/mingwm10.dll
  }else{
    LIBS += -L$${QWT}/lib/ -lqwt5

	app.commands = C:/cygwin/bin/cp.exe \
                        $${QTBINDIR}/QtCore4.dll \
                        $${QTBINDIR}/QtNetwork4.dll \
                        $${QTBINDIR}/QtGui4.dll \
                        $${QTBINDIR}/QtScript4.dll \
						$${QWT}/lib/qwt5.dll \
						XISL.dll \
                        C:/MinGW/bin/mingwm10.dll \
                        release
                        
    zip.commands = C:/cygwin/bin/zip.exe -j $${TARGET}.zip \
			release/$${TARGET}.exe \
                        $${QTBINDIR}/QtCore4.dll \
                        $${QTBINDIR}/QtNetwork4.dll \
                        $${QTBINDIR}/QtGui4.dll \
                        $${QTBINDIR}/QtScript4.dll \
						$${QWT}/lib/qwt5.dll \
						XISL.dll \
                        C:/MinGW/bin/mingwm10.dll
  }
  INCLUDEPATH += $${QWT}/include/
  INCLUDEPATH += .
  INCLUDEPATH += SDK
  
  QMAKE_EXTRA_TARGETS += zip app
  QMAKE_LFLAGS += -Wl,--script,nordata.lscript
  
  LIBS += XISL.lib
}

QMAKE_EXTRA_TARGETS += dox rpmsource mock

dox.target = docs
dox.commands += doxygen
dox.depends = Doxyfile $${SOURCES} $${HEADERS}

rpmsource.commands += make dist ;
rpmsource.commands += cp qxrd@VERSION@.tar.gz ~/rpmbuild/SOURCES/
rpmsource.depends = $${DISTFILES} $${SOURCES} $${FORMS} $${HEADERS} $${RESOURCES}

mock.commands += mock-build qxrd.spec
mock.depends = rpmsource ~/rpmbuild/SOURCES/qxrd@VERSION@.tar.gz qxrd.spec
